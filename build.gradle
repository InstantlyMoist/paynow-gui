plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.2.2'
    id 'de.eldoria.plugin-yml.bukkit' version '0.8.0'
}

group = 'me.kyllian'
version = '1.0.0'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { 
        url = 'https://jitpack.io'
    }
    maven { url = 'https://repo.opencollab.dev/main/' }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven {
        name = 'lunarclient'
        url = 'https://repo.lunarclient.dev'
    }
}

dependencies {
    compileOnly('org.spigotmc:spigot-api:1.21.8-R0.1-SNAPSHOT')
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    compileOnly 'com.lunarclient:apollo-api:1.2.0'
    compileOnly 'com.lunarclient:apollo-extra-adventure4:1.2.0'
    compileOnly("com.squareup.okhttp3:okhttp:5.2.1")
    compileOnly("com.squareup.okhttp3:logging-interceptor:5.2.1")
    
    implementation 'gg.paynow:java-sdk:1.0.34'
}

compileJava {
    sourceCompatibility = '21'
    targetCompatibility = '21'
    options.compilerArgs += ["-parameters"]
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

bukkit {
    main = "me.kyllian.PayNowGUI.PayNowGUIPlugin"
    apiVersion = "1.21.8"
    authors = ["Kyllian"]
    description = "Unofficial Spigot plugin for the PayNow API"

    softDepend = [ "Apollo-Bukkit" ]

    commands {
        buy {
            usage = "/<command>"
        }
    }
}

shadowJar {
    // produce a plugin-ready jar without classifier/version
    archiveBaseName.set(project.name)
    archiveClassifier.set('')
    archiveVersion.set('')
    
    dependencies {
        include(dependency('gg.paynow:java-sdk:.*'))
        include(dependency('io.gsonfire:gson-fire:.*'))
    }
    
    // relocate external package to avoid classpath conflicts
    relocate 'gg.paynow', 'me.kyllian.shaded.paynow'
    
    // exclude unnecessary files to reduce size
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE*'
    exclude 'META-INF/NOTICE*'
    exclude 'META-INF/maven/**'
    exclude '**/*.kotlin_metadata'
    exclude '**/*.kotlin_builtins'
    
    // put the built plugin into the TEST_SERVER plugins directory when present
    destinationDirectory.set(file("${System.getenv('TEST_SERVER')}/plugins"))
}

// Disable the plain jar to avoid publishing it by accident
tasks.named('jar') {
    enabled = false
}

task uploadJar(type: Exec) {
    dependsOn shadowJar
    description = 'Uploads shaded jar to remote server via SSH using scp.'
    def remoteServer  = System.getenv('TEST_HOST')
    def remoteDir  = System.getenv('PP_REMOTE_DIR')
    // echo
    doFirst {
        commandLine 'scp', "${shadowJar.archiveFile.get().asFile.absolutePath}", "${remoteServer}:${remoteDir}"
    }
}
